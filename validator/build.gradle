group 'io.lindstrom'
version '1.0-SNAPSHOT'

apply plugin: 'java'

ext.moduleName = 'io.lindstrom.mpd.validator'


compileJava {
    exclude 'module-info.java'
}

task compileModuleInfoJava(type: JavaCompile) {
    classpath = files() // empty
    source = 'src/main/java/module-info.java'
    destinationDir = compileJava.destinationDir // same dir to see classes compiled by compileJava

    doFirst {
        options.compilerArgs = [
                '--release', '9',
                '--module-path', compileJava.classpath.asPath,
        ]
    }
}

compileModuleInfoJava.dependsOn compileJava
classes.dependsOn compileModuleInfoJava

repositories {
    mavenCentral()
}

dependencies {
    compile project(':parser')

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.5.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.5.1")
    testCompile("org.junit.jupiter:junit-jupiter-params:5.5.1")
    testCompile 'io.github.benas:random-beans:3.7.0'
}
test {
    useJUnitPlatform()
}
